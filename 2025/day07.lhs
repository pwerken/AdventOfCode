--- Day 7: Laboratories ---

You thank the cephalopods for the help and exit the trash compactor, finding
yourself in the familiar halls of a North Pole research wing.

Based on the large sign that says "teleporter hub", they seem to be
researching teleportation; you can't help but try it for yourself and step
onto the large yellow teleporter pad.

Suddenly, you find yourself in an unfamiliar room! The room has no doors; the
only way out is the teleporter. Unfortunately, the teleporter seems to be
leaking magic smoke.

Since this is a teleporter lab, there are lots of spare parts, manuals, and
diagnostic equipment lying around. After connecting one of the diagnostic
tools, it helpfully displays error code 0H-N0, which apparently means that
there's an issue with one of the tachyon manifolds.

You quickly locate a diagram of the tachyon manifold (your puzzle input). A
tachyon beam enters the manifold at the location marked S; tachyon beams
always move downward. Tachyon beams pass freely through empty space (.).
However, if a tachyon beam encounters a splitter (^), the beam is stopped;
instead, a new tachyon beam continues from the immediate left and from the
immediate right of the splitter.

For example:

    .......S.......
    ...............
    .......^.......
    ...............
    ......^.^......
    ...............
    .....^.^.^.....
    ...............
    ....^.^...^....
    ...............
    ...^.^...^.^...
    ...............
    ..^...^.....^..
    ...............
    .^.^.^.^.^...^.
    ...............

In this example, the incoming tachyon beam (|) extends downward from S until
it reaches the first splitter:

    .......S.......
    .......|.......
    .......^.......
    ...............
    ......^.^......
    ...............
    .....^.^.^.....
    ...............
    ....^.^...^....
    ...............
    ...^.^...^.^...
    ...............
    ..^...^.....^..
    ...............
    .^.^.^.^.^...^.
    ...............

At that point, the original beam stops, and two new beams are emitted from the
splitter:

    .......S.......
    .......|.......
    ......|^|......
    ...............
    ......^.^......
    ...............
    .....^.^.^.....
    ...............
    ....^.^...^....
    ...............
    ...^.^...^.^...
    ...............
    ..^...^.....^..
    ...............
    .^.^.^.^.^...^.
    ...............

Those beams continue downward until they reach more splitters:

    .......S.......
    .......|.......
    ......|^|......
    ......|.|......
    ......^.^......
    ...............
    .....^.^.^.....
    ...............
    ....^.^...^....
    ...............
    ...^.^...^.^...
    ...............
    ..^...^.....^..
    ...............
    .^.^.^.^.^...^.
    ...............

At this point, the two splitters create a total of only three tachyon beams,
since they are both dumping tachyons into the same place between them:

    .......S.......
    .......|.......
    ......|^|......
    ......|.|......
    .....|^|^|.....
    ...............
    .....^.^.^.....
    ...............
    ....^.^...^....
    ...............
    ...^.^...^.^...
    ...............
    ..^...^.....^..
    ...............
    .^.^.^.^.^...^.
    ...............

This process continues until all of the tachyon beams reach a splitter or exit
the manifold:

    .......S.......
    .......|.......
    ......|^|......
    ......|.|......
    .....|^|^|.....
    .....|.|.|.....
    ....|^|^|^|....
    ....|.|.|.|....
    ...|^|^|||^|...
    ...|.|.|||.|...
    ..|^|^|||^|^|..
    ..|.|.|||.|.|..
    .|^|||^||.||^|.
    .|.|||.||.||.|.
    |^|^|^|^|^|||^|
    |.|.|.|.|.|||.|

To repair the teleporter, you first need to understand the beam-splitting
properties of the tachyon manifold. In this example, a tachyon beam is split a
total of 21 times.

Analyze your manifold diagram. How many times will the beam be split?

>
> import Helpers
>
> type Lasers = [Int]
> type Splitters = [Int]
> type Problem = (Lasers, [Splitters])
>
> parse :: String -> Problem
> parse = mapTuple (indexesOf 'S') (map (indexesOf '^')) . uncons . lines
>   where
>     indexesOf c = map fst . filter ((==) c . snd) . zipWith (,) [1..]
>
> moveBeam :: Lasers -> Splitters -> Lasers
> moveBeam ls ss = dedup . concatMap (flip beamHitSplitter ss) $ ls
>   where
>     beamHitSplitter :: Int -> Splitters -> Lasers
>     beamHitSplitter l ss
>       | l `elem` ss = [l - 1, l + 1]
>       | otherwise   = [l]
>
> shootLaser :: Problem -> [Lasers]
> shootLaser = uncurry (scanl moveBeam)
>
> countHits :: Problem -> Int
> countHits p@(_, gr) = sum $ zipWith rowHit (shootLaser p) gr
>   where
>     rowHit :: Lasers -> Splitters -> Int
>     rowHit ls ss = length . filter id . map (flip elem ss) $ ls
>
> day07p1 = solve "07" (countHits . parse)
>
> e = unlines $
>   [ ".......S......."
>   , "..............."
>   , ".......^......."
>   , "..............."
>   , "......^.^......"
>   , "..............."
>   , ".....^.^.^....."
>   , "..............."
>   , "....^.^...^...."
>   , "..............."
>   , "...^.^...^.^..."
>   , "..............."
>   , "..^...^.....^.."
>   , "..............."
>   , ".^.^.^.^.^...^."
>   , "..............."
>   ]

--- Part Two ---

With your analysis of the manifold complete, you begin fixing the teleporter.
However, as you open the side of the teleporter to replace the broken
manifold, you are surprised to discover that it isn't a classical tachyon
manifold - it's a quantum tachyon manifold.

With a quantum tachyon manifold, only a single tachyon particle is sent
through the manifold. A tachyon particle takes both the left and right path of
each splitter encountered.

Since this is impossible, the manual recommends the many-worlds interpretation
of quantum tachyon splitting: each time a particle reaches a splitter, it's
actually time itself which splits. In one timeline, the particle went left,
and in the other timeline, the particle went right.

To fix the manifold, what you really need to know is the number of timelines
active after a single particle completes all of its possible journeys through
the manifold.

In the above example, there are many timelines. For instance, there's the
timeline where the particle always went left:

    .......S.......
    .......|.......
    ......|^.......
    ......|........
    .....|^.^......
    .....|.........
    ....|^.^.^.....
    ....|..........
    ...|^.^...^....
    ...|...........
    ..|^.^...^.^...
    ..|............
    .|^...^.....^..
    .|.............
    |^.^.^.^.^...^.
    |..............

Or, there's the timeline where the particle alternated going left and right at
each splitter:

    .......S.......
    .......|.......
    ......|^.......
    ......|........
    ......^|^......
    .......|.......
    .....^|^.^.....
    ......|........
    ....^.^|..^....
    .......|.......
    ...^.^.|.^.^...
    .......|.......
    ..^...^|....^..
    .......|.......
    .^.^.^|^.^...^.
    ......|........

Or, there's the timeline where the particle ends up at the same point as the
alternating timeline, but takes a totally different path to get there:

    .......S.......
    .......|.......
    ......|^.......
    ......|........
    .....|^.^......
    .....|.........
    ....|^.^.^.....
    ....|..........
    ....^|^...^....
    .....|.........
    ...^.^|..^.^...
    ......|........
    ..^..|^.....^..
    .....|.........
    .^.^.^|^.^...^.
    ......|........

In this example, in total, the particle ends up on 40 different timelines.

Apply the many-worlds interpretation of quantum tachyon splitting to your
manifold diagram. In total, how many different timelines would a single
tachyon particle end up on?

>
> type Lasers2 = [(Int, Int)]
> type Problem2 = (Lasers2, [Splitters])
>
> parse2 :: String -> Problem2
> parse2 = mapFst (map (flip (,) 1)) . parse
>
> moveBeam2 :: Lasers2 -> Splitters -> Lasers2
> moveBeam2 ls ss = mergeLasers . concatMap (flip beamHitSplitter ss) $ ls
>   where
>     beamHitSplitter :: (Int, Int) -> Splitters -> Lasers2
>     beamHitSplitter (l, i) ss
>       | l `elem` ss = [(l - 1, i), (l + 1, i)]
>       | otherwise   = [(l, i)]
>
>     mergeLasers :: Lasers2 -> Lasers2
>     mergeLasers [] = []
>     mergeLasers [x] = [x]
>     mergeLasers ((a,i):(b,j):cs)
>       | a == b    =         mergeLasers ((b, i + j):cs)
>       | otherwise = (a,i) : mergeLasers ((b,j):cs)
>
> shootLaser2 :: Problem2 -> Lasers2
> shootLaser2 = uncurry (foldl moveBeam2)
>
> countManifold :: Lasers2 -> Int
> countManifold = sum . map snd
>
> day07p2 = solve "07" (countManifold . shootLaser2 . parse2)
